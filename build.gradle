plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT" apply false
    id "dev.architectury.loom" version "1.11-SNAPSHOT" apply false
    id "io.github.juuxel.loom-quiltflower" version "1.7.1" apply false
    id "com.matthewprenger.cursegradle" version "1.+"
    id "com.modrinth.minotaur" version "2.+"
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    group = mod_group_id
    version = mod_version
    archivesBaseName = mod_id

    repositories {
        // Standard repositories
        mavenLocal()
        mavenCentral()

        // Flat directory for local mods
        flatDir { dirs "mods" }

        // Our publishing repo
        maven { url = "https://registry.somethingcatchy.net/repository/maven-releases/" }

        maven { url = "https://api.modrinth.com/maven" }
        maven { url = "https://www.cursemaven.com" }
        maven { url = "https://jitpack.io" }

        maven { url = "https://maven.neoforged.net" }
        maven { url = "https://maven.architectury.dev" }
        maven { url = "https://maven.parchmentmc.org" }

        maven { url = "https://maven.createmod.net" } // Create Mod, Ponder, Flywheel
        maven { url = "https://maven.blamejared.com" } // JEI, Vazkii's Mods
        maven { url = "https://maven.ladysnake.org/releases" } // Ladysnake mods
        maven { url = "https://maven.tterrag.com/" } // Flywheel, EnderIO
        maven { url = "https://mvn.devos.one/releases/" } // Registrate, Porting Lib (releases)
        maven { url = "https://mvn.devos.one/snapshots/" } // Registrate, Porting Lib (snapshots)
        maven { url = "https://maven.terraformersmc.com/" } // TerraformersMC mods
        maven { url = "https://maven.saps.dev/releases" } // FTB Mods
        maven { url = "https://dl.cloudsmith.io/public/tslat/sbl/maven/"}

        maven { url = "https://maven.theillusivec4.top/" } // Curios API
        maven { url = "https://maven.squiddev.cc" } // CC: Tweaked
        maven { url = "https://maven.su5ed.dev/releases" } // SU5ED mods
        maven { url = "https://harleyoconnor.com/maven" } // Dynamic Trees
        maven { url = "https://maven.misterpemodder.com/libs-release/" } // ShulkerBoxTooltip
        maven { url = "https://maven.firstdarkdev.xyz/snapshots" } // FirstDarkDev (snapshots)
        maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven" } // NeoForge config API port

        maven { // Reach Entity Attributes
            url = "https://maven.jamieswhiteshirt.com/libs-release"
            content { includeGroup("com.jamieswhiteshirt") }
        }

    }
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(rootProject.java_version.toInteger())
        }
        withSourcesJar()
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
    }
}

architectury {
    minecraft = minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "io.github.juuxel.loom-quiltflower"
    apply plugin: "com.matthewprenger.cursegradle"
    apply plugin: "com.modrinth.minotaur"

    loom { silentMojangMappingsLicense() }

    dependencies {
        minecraft "com.mojang:minecraft:${minecraft_version}"
        mappings loom.layered() {
            it.officialMojangMappings { setNameSyntheticMembers(false) }
            it.parchment("org.parchmentmc.data:parchment-${parchment_version}")
        }
    }

    publishing {
        repositories {
            mavenLocal()

            def nexusToken = providers.environmentVariable("NEXUS_TOKEN")
            def nexusUser = providers.environmentVariable("NEXUS_USER")
            if (nexusToken.isPresent() && nexusUser.isPresent()) {
                maven {
                    url = uri("https://registry.somethingcatchy.net/repository/maven-releases/")
                    credentials {
                        username = nexusUser.get()
                        password = nexusToken.get()
                    }
                }
            }
        }
    }

    // --- Release metadata & per-loader config
    def modLoader = project.name
    def changelogFile = rootProject.file("changelog.md")
    def changelogText = changelogFile.exists() ? changelogFile.text : ""
    def mcVersions = listGameVersions(minecraft_min_version, minecraft_version)
    def cfToken = providers.environmentVariable("CURSEFORGE_TOKEN")
    def mrToken = providers.environmentVariable("MODRINTH_TOKEN")

    if (mrToken.isPresent()) {
        modrinth {
            projectId = mod_id
            uploadFile = tasks.remapJar
            gameVersions = mcVersions
            if (modLoader == "forge") {
                loaders = ["neoforge"]
            } else loaders = [modLoader]
            changelog = changelogText
            versionNumber = mod_version + "-" + modLoader
            versionType = release_type
            if (modLoader == "fabric") {
                dependencies {
                    required.project "fabric-api"
                    required.project "moonlight"
                }
            } else {
                dependencies {
                    required.project "moonlight"
                }
            }
        }
    }

    if (cfToken.isPresent()) {
        curseforge {
            apiKey = cfToken.get()
            project {
                id = cf_project_id
                changelog = changelogText
                releaseType = release_type
                for (var ver : mcVersions) {
                    addGameVersion ver
                }

                if (modLoader == "neoforge") {
                    addGameVersion("NeoForge")
                } else addGameVersion modLoader.capitalize()

                mainArtifact(tasks.remapJar)

                if (modLoader == "fabric") {
                    relations {
                        requiredDependency "fabric-api"
                        requiredDependency "selene"

                        optionalDependency "the-bumblezone-fabric"
                        optionalDependency "decorative-blocks"
                        optionalDependency("flan")
                        optionalDependency("multi-item-lib")
                        optionalDependency("snowy-spirit")
                        optionalDependency("haunted-harvest")
                        optionalDependency("model-gap-fix")
                        optionalDependency("map-atlases")
                        optionalDependency("farmers-delight-fabric")
                    }
                } else {
                    relations {
                        requiredDependency("selene")

                        optionalDependency("create")
                        optionalDependency("quark")
                        optionalDependency("cc-tweaked")
                        optionalDependency("waystones")
                        optionalDependency("roughly-enough-items")
                        optionalDependency("jei")
                        optionalDependency("model-gap-fix")
                        optionalDependency("flywheel")
                        optionalDependency("configured")
                        optionalDependency("flan-forge")
                        optionalDependency("haunted-harvest")
                        optionalDependency("snowy-spirit")
                        optionalDependency("map-atlases-forge")
                        optionalDependency("decorative-blocks")
                        optionalDependency("farmers-delight")
                        optionalDependency("the-bumblezone-forge")
                        optionalDependency("map-atlases-forge")
                    }
                }
            }
        }
    }

}

ext.replaceProperties = [
        minecraft_version    : minecraft_version,
        minecraft_min_version: minecraft_min_version,
        minecraft_max_version: minecraft_max_version,
        pack_format_number   : pack_format_number,

        mod_id               : mod_id,
        mod_name             : mod_name,
        mod_version          : mod_version,
        mod_license          : mod_license,
        mod_authors          : mod_authors,
        mod_description      : mod_description,
        mod_credits          : mod_credits,
        mod_homepage         : mod_homepage,
        mod_github           : mod_github,

        neo_version          : neo_version,
        neo_version_range    : neo_version_range,
        loader_version_range : loader_version_range,

        moonlight_min_version: moonlight_min_version,
]

// --- Utils
static List<String> listGameVersions(String minInclusive, String maxExclusive) {

    def minParts = minInclusive.split('\\.')
    def maxParts = maxExclusive.split('\\.')

    if (minParts.size() < 2 || minParts.size() > 3 ||
            maxParts.size() < 2 || maxParts.size() > 3) {
        throw new IllegalArgumentException("Version must be MAJOR.MINOR or MAJOR.MINOR.PATCH")
    }

    def minNums = minParts.collect { it as int }
    def maxNums = maxParts.collect { it as int }

    while (minNums.size() < 3) minNums << 0
    while (maxNums.size() < 3) maxNums << 0

    if (minNums[0] != maxNums[0] || minNums[1] != maxNums[1]) {
        throw new IllegalArgumentException("Major and minor versions must match")
    }

    int startPatch = minNums[2]
    int endPatch   = maxNums[2]

    def versions = []

    for (int i = startPatch; i < endPatch; i++) {
        if (i == 0) {
            versions << "${minNums[0]}.${minNums[1]}"
        } else {
            versions << "${minNums[0]}.${minNums[1]}.${i}"
        }
    }

    return versions
}


tasks.register('buildAndPublishAll') {
    dependsOn ':clean'
    dependsOn ':build'
    dependsOn ':neoforge:curseforge'
    dependsOn ':neoforge:modrinth'
    dependsOn ':fabric:curseforge'
    dependsOn ':fabric:modrinth'
    dependsOn ':publish'

    group = 'build'
    description = 'Runs clean, build, and publish neoforge and fabric'
}
